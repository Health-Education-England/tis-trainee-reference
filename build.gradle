plugins {
  id "java"
  id "org.springframework.boot" version "2.5.1"
  id "io.spring.dependency-management" version "1.0.11.RELEASE"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.3"
}

group = "uk.nhs.hee.tis.trainee"
version = "0.5.0"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
  implementation "org.springframework.boot:spring-boot-starter-web"
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  ext.mapStructVersion = "1.4.2.Final"
  implementation "org.mapstruct:mapstruct:$mapStructVersion"
  annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

  // Mongock
  ext.mongockVersion = "4.3.8"
  implementation "com.github.cloudyrock.mongock:mongock-spring-v5:${mongockVersion}"
  implementation "com.github.cloudyrock.mongock:mongodb-springdata-v3-driver:${mongockVersion}"

  // Sentry reporting
  implementation "io.sentry:sentry-spring-boot-starter:5.0.1"

  // Swagger
  ext.swaggerVersion = "3.0.0"
  implementation "io.springfox:springfox-swagger2:$swaggerVersion"
  implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"
}

checkstyle {
  toolVersion = "8.29"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"

    property "sonar.projectKey", "Health-Education-England_tis-trainee-reference"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
